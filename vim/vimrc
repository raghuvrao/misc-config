scriptencoding utf-8

" ~/.vimrc ~/.vim/vimrc
" Author: Raghu V. Rao <raghu.v.rao@gmail.com>

" Useful when starting Vim as ex.
set nocompatible

set nobackup

set noundofile

set nomodeline
set modelines=0

let mapleader = '\'
let maplocalleader = '_'

set backspace=indent,eol,start

let &softtabstop=&tabstop

set nowrap

set sidescroll=1

" Do not allow the cursor to move onto the extends/precedes listchars.
set sidescrolloff=1

" Time out on terminal-key-codes, and not on mappings.
set ttimeout
set notimeout

set nowildmenu
set wildmode=longest,list,full

" Show line and column numbers even when statusline is disabled.
set ruler

set showcmd

set foldmethod=indent
set foldlevelstart=99

set incsearch

set list
set listchars=extends:#,precedes:#,nbsp:%,tab:•\ ,trail:¬

set laststatus=2
set statusline=%(\ %h%w%q%)%(\ [%R%M]%)%(\ %f%)%(\ %y%)%(\ %{&fileformat}%)%(\ %P\ %l,%v\ %)

set report=0

set nofixendofline

set autoindent

set history=500

set nostartofline

set noequalalways

if isdirectory(expand('$HOME/misc-config'))
    set path+=$HOME/misc-config/**
endif
if isdirectory(expand('$HOME/misc-scripts'))
    set path+=$HOME/misc-scripts/**
endif

if !has('gui_running')
    set mouse=
    " Do not meddle with the terminal's cursor.
    set t_ve=
    set t_vi=
    set t_vs=
    set t_VS=
    set t_SI=
    set t_SR=
    set t_EI=
    set t_SC=
    set t_EC=
    set t_SH=
    set t_RC=
    set t_RS=
    if &term =~# '^\%(screen\|tmux\)'
        " :help xterm-mouse, :help sgr-mouse
        set ttymouse=sgr
        " :help arrow_modifiers
        execute "set <xUp>=\<Esc>[@;*A"
        execute "set <xDown>=\<Esc>[@;*B"
        execute "set <xRight>=\<Esc>[@;*C"
        execute "set <xLeft>=\<Esc>[@;*D"
        " :help xterm-bracketed-paste
        let &t_BD = "\<Esc>[?2004l"
        let &t_BE = "\<Esc>[?2004h"
        let &t_PS = "\<Esc>[200~"
        let &t_PE = "\<Esc>[201~"
    endif
endif

set display+=lastline

set formatoptions=1jlmnoqr

filetype plugin indent on

runtime ftplugin/man.vim
let g:ft_man_open_mode = 'tab'
set keywordprg=:Man

" Delete the vimrcEx and vimStartup autocommand groups.
augroup vimrcEx
    autocmd!
augroup END
augroup! vimrcEx
augroup vimStartup
    autocmd!
augroup END
augroup! vimStartup

function! JumpToLastExitPosition() abort
    if getcurpos()[1:2] == [1, 1]
                \ && expand('%:p') !=# ''
                \ && !(&diff)
                \ && &buftype ==# ''
                \ && &filetype !=# 'git'
                \ && &filetype !=# 'gitcommit'
                \ && &filetype !=# 'gitrebase'
                \ && &filetype !=# 'hgcommit'
                \ && &filetype !=# 'man'
                \ && &filetype !=# 'svn'
        let x = line("'\"")
        if 1 <= x && x <= line('$')
            normal! g`"zz
        endif
    endif
endfunction
augroup jump_to_last_exit_position
    autocmd!
    autocmd BufReadPost * call JumpToLastExitPosition()
augroup END

function! PutCurrentTimestamp(unixt = 0) abort
    put =strftime(a:unixt ? '%s' : '%F %a %T %Z(UTC%z)')
endfunction
command! -nargs=? PutCurrentTimestamp call PutCurrentTimestamp(<args>)

if executable('xsel')
    nnoremap <Leader>y <Cmd>%write !xsel -ib 2>/dev/null<CR>
    xnoremap <silent> <Leader>y <Cmd>write !xsel -ib 2>/dev/null<CR>
    nnoremap <Leader>p <Cmd>read !xsel -ob 2>/dev/null<CR>
endif

nnoremap <Leader>I <Cmd>edit $MYVIMRC<CR>

nnoremap <Leader>[ <Cmd>ToggleMatchParen<CR>

nnoremap <Leader>a <Cmd>buffer #<CR>
nnoremap <Leader>z <Cmd>bprevious<CR>
nnoremap <Leader>x <Cmd>bnext<CR>

nnoremap <Leader>S :<C-U>%substitute/\<<C-R><C-W>\>//g<Left><Left>
nnoremap <Leader>s :<C-U>%substitute/\</g<Left><Left>
xnoremap <Leader>s :substitute/\</g<Left><Left>

nnoremap <Leader>g :<C-U>%global/\<
xnoremap <Leader>g :global/\<
nnoremap <Leader>v :<C-U>%vglobal/\<
xnoremap <Leader>v :vglobal/\<

nnoremap <Leader><Leader> <Cmd>update<CR>
xnoremap <Leader><Leader> <Cmd>update<CR>

nnoremap <Leader>D <Cmd>help digraph-table-mbyte<CR>
nnoremap <Leader>d <Cmd>help digraph-table<CR>

nnoremap <Leader>w <Cmd>set invwrap<CR>

nnoremap <Leader>h <Cmd>nohlsearch<CR>

nnoremap <Leader><CR> <Cmd>call maxtab#ToggleMaximizeToTab()<CR>

nnoremap <Leader>] <Cmd>echo synids#SynIDs(line('.'), col('.'))<CR>

function! RedrawLineNearEdge(edge, percentage) abort
    if &wrap
        echoerr "Not implemented when 'wrap' is set"
        return
    endif
    if a:percentage < 0 || a:percentage > 100
        echoerr "Percentage" a:percentage "is not in the interval [0, 100]"
        return
    endif
    let wh = winheight(0)
    let lc = float2nr((wh * a:percentage) / 100)
    let wl = winline()
    if a:edge == 0 " Top edge
        let c = wl - lc - 1
        if c == 0
            return
        elseif c < 0
            execute 'normal!' .. -(c) .. "\<C-Y>"
        else
            execute 'normal!' .. c .. "\<C-E>"
        endif
    else " Bottom edge
        let c = wh - lc - wl
        if c == 0
            return
        elseif c < 0
            execute 'normal!' .. -(c) .. "\<C-E>"
        else
            execute 'normal!' .. c .. "\<C-Y>"
        endif
    endif
endfunction
nnoremap <silent> zB <Cmd>call RedrawLineNearEdge(1, 12)<CR>
nnoremap <silent> zT <Cmd>call RedrawLineNearEdge(0, 12)<CR>
xnoremap <silent> zB <Cmd>call RedrawLineNearEdge(1, 12)<CR>
xnoremap <silent> zT <Cmd>call RedrawLineNearEdge(0, 12)<CR>

nnoremap <Leader>T :<C-U>tab terminal ++close<Space>

inoremap <C-W> <C-G>u<C-W>
inoremap <C-U> <C-G>u<C-U>
inoremap <C-M> <C-G>u<C-M>
inoremap <NL> <C-G>u<NL>
inoremap <C-J> <C-G>u<C-J>
inoremap <CR> <C-G>u<CR>

runtime vimrc_local.vim

" Do not highlight numbers, characters, and strings in C/C++ comments.
unlet! c_comment_strings

syntax on
colorscheme raghuvrao

packadd! matchit

" :help trojan-horse
set secure
set noexrc
